apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
//apply from: '../config.gradle'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        kapt {
            arguments {
                arg("AROUTER_MODULE_NAME", project.getName())
            }
        }
    }
    resourcePrefix "common_"
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

repositories {
    mavenCentral()
}

def implementationDependencies = rootProject.ext.dependencies
def compiler = rootProject.ext.compiler

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    api project(':flutter')
    api project(':flutter_boost')
    api project(':lib_banner')

    implementationDependencies.each { k, v -> api v }

    kapt compiler['glide-compiler']
    kapt compiler['arouter-compiler']
    kapt compiler['room-compiler']

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
}

